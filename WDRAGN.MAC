	.TITLE WDRAGN		;DRAGON FOR WARLORDS
	.INCLUDE WDCOMN
	.INCLUDE HLL65
	.CSECT

DRAGON:
	LDA DG.COM
	IFNE			;?ARE WE BUSY?
	LDA I,8
	STA LA.CNT
	LDA DG.TRN
	IFNE
	JMP DGTURN
	ENDIF
	LDX DG.PHS		;GET CURRENT PHASE
	LDA X,TPHASE+1		;GOTO TO PHASE ROUTINE
	PHA
	LDA X,TPHASE
	PHA
	ENDIF
	RTS			;PHASE FINISH WITH RTS

TPHASE:				;TABLE OF DRAGON PHASES
	IPHSBG=0
	.WORD DGBGIN-1		;0;BEGIN WHOLE PROCESS
	IPHSEN=2
	.WORD DGENTR-1		;2;ENTER THE SCREEN
	IPHSSR=4
	.WORD DGSRCH-1		;4; SEARCH BACK AND FORTH
	IPHSTW=6
	.WORD DGTWRL-1		;6;TWIRL AROUND IN PLACE
	IPHSFL=8
	.WORD DGFLAM-1		;8;FLAME AT PLAYER
	IPHSLV=0A
	.WORD DGLEAV-1		;0A;LEAVE SCENE OF CRIME

DGBGIN:				;BEGIN LAUNCHING THE DRAGON
				;DETERMINE LUCKY RECEPIENT
	LDA $INTCT		;DO NOT USE 'JUST STARTED' POKEY
	ROL
	ROL
	ROL
	AND I,3
	TAX			;START AT RANDOM CASTLE
	BEGIN			;LOOP TILL PLAYER FOUND
	DEX
	IFMI			;?WRAP AROUND?
	LDX I,3			;THEN START AT THE TOP
	ENDIF
	LDA PLAYER
	AND X,BITTST
	NEEND
	STX DG.PLN		;SAVE THE PLAYER NUMBER
	CPX I,2
	IFCS			;?SHOOT AT PLAYERS ON TOP?
	LDA I,8
	STA DG.PSY		;LOCATION
	LDA I,-1
	STA DG.DRY		;DIRECTION TO FACE-UP FLIPPED
	LDA I,80
	STA DG.FLP		;PICTURE FLIPPED MASK
	LDA I,2
	STA DG.VY
	ELSE			;ELSE SHOOT DOWN
	LDA I,8
	STA DG.PSY		;LOCATION
	LDA I,0
	STA DG.DRY		;DIRECTION TO FACE-DOWN NORMAL
	LDA I,0
	STA DG.FLP		;NORMAL VIEW
	LDA I,-2
	STA DG.VY		;FALL DOWN
	ENDIF
	LDA I,0
	STA DG.VX		;NO LEFT RIGHT MOVEMENT
	LDA I,0			;LEAVES FLP ALONE
	STA DG.DRX
	LDA I,80
	STA DG.PSX		;POSITION IN CENTER X
	LDA I,0
	STA DG.STM		;RESTART SEQUENCE TIMER
	STA DG.SPT
	LDA I,ISEQEN
	STA DG.SBS		;SET SEQUENCE BASE TO DGENTRY
	LDA I,IPHSEN
	STA DG.PHS		;SET PHASE=DGENTRY
	RTS

DGENTR:
	JSR DGMOVE		;MOVE THE DRAGON
	JSR DGSEQ		;PROCESS VIEWING SEQUENCE
	JSR DGDRAW		;DRAW THE DRAGON
	LDA DG.PSY
	CMP I,58
	IFCS			;?ABOVE THE MIDLINE?
	CMP I,22.*8+4
	IFCC			;?AND NEAR IT?
	LDA I,IPHSSR		;THEN CHANGE PHASE TO SEARCHING
	STA DG.PHS
	LDA I,ISEQSR
	STA DG.SBS
	LDA I,-1		;ALWAYS START TO LEFT
	STA DG.VX
	LDA I,80		;PROCEED WITH TURN
	STA DG.TRN
	ENDIF
	ENDIF
	RTS

DGSRCH:
	LDA I,0
	STA DG.VY		;NO VERTICAL MOVEMENT
	JSR DGMOVE
	JSR DGSEQ
	JSR DGDRAW
	LDA DG.PSX
	CMP I,1C
	BCC 20$			;B?TOO FAR LEFT?
	CMP I,0DC
	IFCS			;?TOO FAR TO RIGHT?
20$:				;THEN TURN DRAGON AROUND
	EOR DG.VX
	IFMI			;?GOING TOWARD EDGE?
	LDA I,80
	STA DG.TRN
	ENDIF
	ENDIF
	LDA DG.COM
	IFMI			;?LAUNCH BALL ASAP?
	LDA DG.PSX
	CMP I,15.*8
	IFCS			;?TO RIGHT OF CENTER?
	CMP I,17.*8
	IFCC			;?AND NEAR IT?
	LDA I,IPHSTW		;THEN SET PHASE=TWIRL
	STA DG.PHS
	LDA I,0
	STA DG.STM
	STA DG.SPT
	LDA I,ISEQTW
	STA DG.SBS
	LDA I,0			;STAY IN PLACE
	STA DG.VX
	STA DG.VY
	ENDIF
	ENDIF
	ENDIF
	RTS

DGTWRL:				;TWIRL AROUND, AIM AT PLAYER
	JSR DGSEQ
	JSR DGDRAW
	LDA DG.STM
	IFEQ			;?DONE WITH CURRENT PIE?
	LDX DG.SPT
	IFEQ			;?DONE WITH CURRENT SEQUENCE?
	LDA DG.DRY		;CHECK WHICH PLAYER WE ARE FACING
	ROL
	LDA DG.DRX
	ROL			;X INTO CARRY
	ROL			;LO BITS=Y,X
	AND I,3
	CMP DG.PLN
	IFEQ			;?FACING CORRECT PLAYER?
	LDA I,IPHSFL
	STA DG.PHS
	LDA I,ISEQFL
	STA DG.SBS
	JMP DGLAUNCH
	ENDIF
	LDA I,80
	STA DG.TRN
	ENDIF
	ENDIF
	RTS

DGFLAM:
	JSR DGSEQ
	JSR DGDRAW
	LDA DG.STM
	IFEQ			;?FINISHED FLAMING?
	LDA DG.SPT
	IFEQ
	LDA I,IPHSLV		;THEN LEAVE THE SCENE
	STA DG.PHS
	LDA I,ISEQLV
	STA DG.SBS
	LDA I,0
	LDY DG.DRX
	IFPL			;?FACING LEFT?
	LDA I,0
	ENDIF
	STA DG.VX
	LDA I,4
	LDY DG.DRY
	IFPL			;?GOING DOWN?
	LDA I,-4
	ENDIF
	STA DG.VY
	ENDIF
	ENDIF
	RTS

DGLEAV:
	JSR DGMOVE
	JSR DGSEQ
	JSR DGDRAW
	LDA DG.PSY
	CMP I,5.
	IFCS			;?NEAR TOP?
	CMP I,12.
	IFCC			;?AND NEAR BOTTOM?
	JMP DGEND		;THEN TERMINATE DRAGAN
	ENDIF
	ENDIF
	RTS

DGEND:
	LDX I,7			;DISAPPEAR THE DRAGON
	LDA I,0
	BEGIN
	STA X,DGXDIS
	STA X,DGYDIS
	STA X,DGPDIS
	STA X,M.HPICT+8
	DEX
	MIEND
	STA DG.PHS		;BEGIN PHASE
	STA DG.TRN		;NO TURNING
	STA DG.COM		;DRAGON ALL DONE
	STA DG.STM		;TIMER READY FOR RESET
	STA DG.SPT		;POINTER TO BE RE-ASSIGNED
	RTS

DGMOVE:				;MOVE THE DRAGON
	LDA DG.PSX		;X COORD
	CLC
	ADC DG.VX
	STA DG.PSX
	LDA DG.PSY		;Y COORD
	CLC
	ADC DG.VY
	STA DG.PSY
	RTS

DGDRAW:
	LDX DG.SPC
	LDY I,7
	BEGIN			;LOOP THRU DRAGON'S MOT OBJ
	LDA X,TDGDRP		;PICTURE STAMP
	EOR DG.FLP
	STA Y,DGPDIS
	LDA DG.DRX		;X DIRECTION
	CMP I,80
	EOR X,TDGDRX		;NEGATED IF NECESSARY
	ADC DG.PSX
	STA Y,DGXDIS
	LDA DG.DRY		;Y DIRECTION
	CMP I,80
	EOR X,TDGDRY		;NEGATE IF NEEDED
	ADC DG.PSY
	STA Y,DGYDIS
	INX
	DEY
	MIEND
	LDA DGPDIS+1
	AND I,3F
	CMP I,24
	IFEQ
	LDA FRAME
	AND I,20
	IFNE
	LDA I,2A
	EOR DG.FLP
	STA DGPDIS+1
	LDA I,2B
	EOR DG.FLP
	STA DGPDIS+0
	ENDIF
	ENDIF
	RTS

TDGDRP:				;PICTURE STAMPS
	.BYTE 18,19,20,21,22,23,24,25
	.BYTE 18,19,26,27,28,29,24,25
	.BYTE 18,19,2C,2D,2E,2F,24,25
	.BYTE 1A,1B,20,21,22,23,24,25
	.BYTE 1A,1B,26,27,28,29,24,25
	.BYTE 1A,1B,2C,2D,2E,2F,24,25
	.BYTE 1C,1D,26,27,28,29,24,25
	.BYTE 1C,1D,2C,2D,2E,2F,24,25
	.BYTE 38,39,34,35,36,37,32,33
	.BYTE 3F,3B,3C,3D,3E,3A,0,0

WDHU=0.
WDHU=0.			;WING DOWN HEAD UP
WMHU=8.				;WING MIDDLE
WUHU=16.			;WING UP
WDHD=24.			;HEAD DOWN
WMHD=32.
WUHD=40.
WMHS=48.			;HEAD SPLITTING
WUHS=56.
WMT6=64.			;1/6TH TURN
WMT3=72.			;1/3RD TURN

TDGDRX:				;X DISP TO PICS
	.BYTE -18,-10,-8,-8,0,0,8,10
	.BYTE -18,-10,-8,-8,0,0,8,10
	.BYTE -18,-10,-8,-8,0,0,8,10
	.BYTE -10,-10,-8,-8,0,0,8,10
	.BYTE -10,-10,-8,-8,0,0,8,10
	.BYTE -10,-10,-8,-8,0,0,8,10
	.BYTE -14,-10,-8,-8,0,0,8,10
	.BYTE -14,-10,-8,-8,0,0,8,10
	.BYTE -10,-8,-10,-8,0,8,0,8
	.BYTE -8,-10,-8,0,8,0,0,0

TDGDRY:				;Y DISP TO PICS
	.BYTE -5,-1,0,-8,0,-8,-4,-4
	.BYTE -7,-3,0,-8,0,-8,-7,-7
	.BYTE -9,-5,0,-8,0,-8,-0A,-0A
	.BYTE -0B,-3,0,-8,0,-8,-4,-4
	.BYTE -0D,-5,0,-8,0,-8,-7,-7
	.BYTE -0F,-7,0,-8,0,-8,-0A,-0A
	.BYTE -0E,-6,0,-8,0,-8,-7,-7
	.BYTE -10,-8,0,-8,0,-8,-0A,-0A
	.BYTE -0C,-0C,-4,-4,-4,-4,4,4
	.BYTE -0C,-4,-4,-4,-4,4,0,0

DGSEQ:				;SEQUENCE TO NEXT PICTURE
	DEC DG.STM		;DECREASE TIMER
	IFMI			;?FINISHED WITH CURRENT PICTURE?
	LDX DG.SPT		;POINT TO NEXT PICTURE
	IFEQ			;?FINISHED WITH CURRENT SEQUENCE?
	LDY DG.SBS		;GET THE BASE POINTER
	LDA Y,TBSEQ		;HOW MANY DIFFERENT SEQUENCES
	IFNE
	STA J
	LDA $INTCT
	LSR	;LOW 2 BITS ARE NOT RANDOM
	EOR P.RNDM	;MIX IT UP ONCE POKEY STARTS
	AND I,7
	BEGIN
	LSR
	CMP J
	CCEND
	ENDIF
	SEC			;BASE+OFFSET+1
	ADC DG.SBS
	TAY
	LDX Y,TBSEQ
	STX DG.SPT
	ENDIF
	LDA X,TBFLY
	STA DG.SPC
	LDA X,TBFLY+1
	IFMI			;?LAST PICTURE IN SEQUENCE?
	LDX I,-2		;SET UP FOR VOID SEQUENCE
	ENDIF
	AND I,7F
	STA DG.STM		;SAVE THE TIMER
	INX
	INX
	STX DG.SPT		;SAVE UPDATED POINTER
	JSR DGSND
	ENDIF
	RTS

TBFLY:
GL1:	.BYTE WMT6,82
FS1:	.BYTE WMHU,8,WUHU,0D,WMHU,4,WDHU,0D,WMHD,98
FS2:	.BYTE WMHU,8,WUHU,0D,WMHU,4,WDHU,0D,WMHS,98
FS3:	.BYTE WMHU,8,WUHU,0D,WMHU,4,WDHU,0D,WMHU,98
TW1:	.BYTE WMHS,90
FL1:	.BYTE WUHS,90
LV1:	.BYTE WMHD,4,WMT6,8,WMT3,0FF

TBSEQ=.-1
ISEQEN=.-TBSEQ
	.BYTE 1
	.BYTE GL1-TBFLY
ISEQSR=.-TBSEQ
	.BYTE 3
	.BYTE FS1-TBFLY,FS3-TBFLY,FS2-TBFLY
ISEQTW=.-TBSEQ
	.BYTE 1
	.BYTE TW1-TBFLY
ISEQFL=.-TBSEQ
	.BYTE 1
	.BYTE FL1-TBFLY
ISEQLV=.-TBSEQ
	.BYTE 1
	.BYTE LV1-TBFLY

DGTURN:
	LDA FRAME
	AND I,1
	IFNE			;?TIME TO MOVE AT HALF SPEED
	JSR DGMOVE
	ENDIF
	LDA DG.TRN		;CHECK MODE
	IFMI			;?JUST STARTED?
	LDA I,0
	STA DG.STM		;PREPARE TO RESET TIMER
	STA DG.SPT
	LDA I,11.
	STA DG.TRN
	ENDIF
	DEC DG.STM
	IFMI			;?TIMER FINISHED?
	DEC DG.TRN		;FEWER TURNS LEFT
	IFEQ			;?END OF TURNING?
	LDA I,0			;FORCE END
	STA DG.TRN		;RETURN TO PHASE MOVEMENT
	STA DG.STM
	STA DG.SPT
	JMP DRAGON		;J EXIT
	ENDIF
	LDX DG.SPT
	LDA X,TTRNPC
	IFEQ
	LDA DG.VX		;SLIP X VELOCITY
	EOR I,0FF
	TAY
	INY
	STY DG.VX
	LDA DG.FLP
	EOR I,40		;FLIP STAMPS
	STA DG.FLP
	LDA DG.DRX		;FLIP FARING DIRECTION
	EOR I,0FF
	STA DG.DRX
	INX
	LDA X,TTRNPC
	ENDIF
	INX
	STX DG.SPT
	STA DG.SPC
	LDA I,3
	STA DG.STM
	JSR DGDRAW
	ENDIF
	RTS

TTRNPC:	.BYTE WMHD,WMT6,WMT6,WMT3,WMT3,0
	.BYTE WMT3,WMT3,WMT6,WMT6,WMHD


DGSND:				;OUTPUT SOUND FROM SEQ
	LDA DG.SPC
	CMP I,WMHS
	BEQ 20$			;B IF SPITTING
	CMP I,WUHS
	IFEQ
20$:
	LDY I,3F
	JSR SNDON
	ENDIF
	RTS


	HLL65
	.END
                                                                                                                                                                                                                                                                                                                                                                                                           